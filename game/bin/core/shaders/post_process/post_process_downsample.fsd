// https://learnopengl.com/Guest-Articles/2022/Phys.-Based-Bloom

// This shader performs downsampling on a texture,
// as taken from Call Of Duty method, presented at ACM Siggraph 2014.
// This particular method was customly designed to eliminate
// "pulsating artifacts and temporal stability issues".

// Remember to add bilinear minification filter for this texture!
// Remember to use a floating-point texture format (for HDR)!
// Remember to use edge clamping for this texture!
uniform vec2 src_resolution;
out vec3 FragColour;

void main () {
    vec2 src_texel_size = 1.0 / src_resolution;
    float x = src_texel_size.x;
    float y = src_texel_size.y;

    // Take 13 samples around current texel:
    // a - b - c
    // - j - k -
    // d - e - f
    // - l - m -
    // g - h - i
    // === ('e' is the current texel) ===
    vec3 a = texture(texture_id, vec2(_TexCoord.x - 2*x, _TexCoord.y + 2*y)).rgb;
    vec3 b = texture(texture_id, vec2(_TexCoord.x,       _TexCoord.y + 2*y)).rgb;
    vec3 c = texture(texture_id, vec2(_TexCoord.x + 2*x, _TexCoord.y + 2*y)).rgb;

    vec3 d = texture(texture_id, vec2(_TexCoord.x - 2*x, _TexCoord.y)).rgb;
    vec3 e = texture(texture_id, vec2(_TexCoord.x,       _TexCoord.y)).rgb;
    vec3 f = texture(texture_id, vec2(_TexCoord.x + 2*x, _TexCoord.y)).rgb;

    vec3 g = texture(texture_id, vec2(_TexCoord.x - 2*x, _TexCoord.y - 2*y)).rgb;
    vec3 h = texture(texture_id, vec2(_TexCoord.x,       _TexCoord.y - 2*y)).rgb;
    vec3 i = texture(texture_id, vec2(_TexCoord.x + 2*x, _TexCoord.y - 2*y)).rgb;

    vec3 j = texture(texture_id, vec2(_TexCoord.x - x, _TexCoord.y + y)).rgb;
    vec3 k = texture(texture_id, vec2(_TexCoord.x + x, _TexCoord.y + y)).rgb;
    vec3 l = texture(texture_id, vec2(_TexCoord.x - x, _TexCoord.y - y)).rgb;
    vec3 m = texture(texture_id, vec2(_TexCoord.x + x, _TexCoord.y - y)).rgb;

    // Apply weighted distribution:
    // 0.5 + 0.125 + 0.125 + 0.125 + 0.125 = 1
    // a,b,d,e * 0.125
    // b,c,e,f * 0.125
    // d,e,g,h * 0.125
    // e,f,h,i * 0.125
    // j,k,l,m * 0.5
    // This shows 5 square areas that are being sampled. But some of them overlap,
    // so to have an energy preserving downsample we need to make some adjustments.
    // The weights are the distributed, so that the sum of j,k,l,m (e.g.)
    // contribute 0.5 to the final color output. The code below is written
    // to effectively yield this sum. We get:
    // 0.125*5 + 0.03125*4 + 0.0625*4 = 1
    FragColour =  e*0.125;
    FragColour += (a+c+g+i)*0.03125;
    FragColour += (b+d+f+h)*0.0625;
    FragColour += (j+k+l+m)*0.125;
}