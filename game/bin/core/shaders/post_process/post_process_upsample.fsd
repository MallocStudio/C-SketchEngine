// https://learnopengl.com/Guest-Articles/2022/Phys.-Based-Bloom

// This shader performs upsampling on a texture,
// as taken from Call Of Duty method, presented at ACM Siggraph 2014.

// Remember to add bilinear minification filter for this texture!
// Remember to use a floating-point texture format (for HDR)!
// Remember to use edge clamping for this texture!
uniform float filter_radius;

void main()
{
    // The filter kernel is applied with a radius, specified in texture
    // coordinates, so that the radius will vary across mip resolutions.
    float x = filter_radius;
    float y = filter_radius;

    // Take 9 samples around current texel:
    // a - b - c
    // d - e - f
    // g - h - i
    // === ('e' is the current texel) ===
    vec3 a = texture(texture_id, vec2(_TexCoord.x - x, _TexCoord.y + y)).rgb;
    vec3 b = texture(texture_id, vec2(_TexCoord.x,     _TexCoord.y + y)).rgb;
    vec3 c = texture(texture_id, vec2(_TexCoord.x + x, _TexCoord.y + y)).rgb;

    vec3 d = texture(texture_id, vec2(_TexCoord.x - x, _TexCoord.y)).rgb;
    vec3 e = texture(texture_id, vec2(_TexCoord.x,     _TexCoord.y)).rgb;
    vec3 f = texture(texture_id, vec2(_TexCoord.x + x, _TexCoord.y)).rgb;

    vec3 g = texture(texture_id, vec2(_TexCoord.x - x, _TexCoord.y - y)).rgb;
    vec3 h = texture(texture_id, vec2(_TexCoord.x,     _TexCoord.y - y)).rgb;
    vec3 i = texture(texture_id, vec2(_TexCoord.x + x, _TexCoord.y - y)).rgb;

    // Apply weighted distribution, by using a 3x3 tent filter:
    //  1   | 1 2 1 |
    // -- * | 2 4 2 |
    // 16   | 1 2 1 |
    FragColour =  vec4( e*4.0,         1.0);
    FragColour += vec4( (b+d+f+h)*2.0, 1.0);
    FragColour += vec4( (a+c+g+i),     1.0);
    FragColour *= 1.0 / 16.0;
}